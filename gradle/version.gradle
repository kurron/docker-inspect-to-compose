
/*
 * Copyright (c) 2015. Ronald D. Kurr kurr@jvmguy.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

class ProjectVersion {
    Integer major
    Integer minor
    Integer patch
    String  branch

    ProjectVersion( Integer major, Integer minor, Integer patch, String branch ) {
        this.major = major
        this.minor = minor
        this.patch = patch
        this.branch = branch
    }

    @Override
    String toString() {
        String release = '-SNAPSHOT'
        if ( branch == 'master' ) {
            release = '.RELEASE'
        }
        else if ( branch == 'development' ) {
            release = '.MILESTONE'
        }
        "${major}.${minor}.${patch}${release}"
    }
}

version = new ProjectVersion( major.toInteger(), minor.toInteger(), patch.toInteger(), branch.toString() )

task printVersion() {
    group = 'Help'
    description = 'Prints project version'
    doLast {
        logger.quiet "Version: ${version}"
    }
}

task createBuildInformationFile() {
    group = 'Help'
    description = 'Creates a properties file that contains details about the build'
    doLast {
        def infoFile = file( "${buildDir}/build-info.properties" )
        def properties = new Properties()
        properties.setProperty( 'version', version.toString() )
        properties.setProperty( 'java.version', System.properties['java.version'] )
        properties.setProperty( 'java.vendor', System.properties['java.vendor'] )
        properties.setProperty( 'os.name', System.properties['os.name'] )
        properties.setProperty( 'os.arch', System.properties['os.arch'] )
        properties.setProperty( 'os.version', System.properties['os.version'] )
        properties.store( infoFile.newWriter(), 'Build Information' )
    }
}

